apply plugin: 'com.android.library'

repositories {
    google()
    jcenter()
    mavenCentral()
    maven {
        url 'https://maven.google.com'
    }
    maven { url 'https://jitpack.io' }
    maven {
        credentials {
            username System.getenv("MAVEN_USERNAME")
            password System.getenv("MAVEN_PASSWORD")
        }
        url 'https://dl.bintray.com/applicaster-ltd/maven'
    }
}

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'com.github.dcendents:android-maven-plugin:1.2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    }
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.2'

    compileOptions.encoding = 'ISO-8859-1'

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 27

        consumerProguardFiles 'proguard-rules.txt'
    }

    packagingOptions {
        exclude 'jsr305_annotations/Jsr305_annotations.gwt.xml'
    }

    lintOptions {
        abortOnError false
    }

    // As our SDK uses proguard, we need to include this as otherwise will fail with an error when performing
    // the tests when using the closed SDK.
    // (https://github.com/robolectric/robolectric-gradle-plugin/issues/144#issuecomment-265899560)
    testOptions {
        unitTests {
            all {
                jvmArgs '-noverify'
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}



dependencies {
    // Applicaster SDK
    def applicasterSDKPath = 'com.applicaster:applicaster-android-sdk:2.32.16'

    //Check if an open SDK is defined - if not use the closed one.
    def devSDK = findProject(':applicaster-android-sdk')
    if (devSDK != null) {
        implementation project(':applicaster-android-sdk')
    } else {
        implementation(applicasterSDKPath)
    }

    api 'io.reactivex.rxjava2:rxandroid:2.0.2'

    api 'com.squareup.retrofit2:retrofit:2.4.0'
    api 'com.squareup.retrofit2:converter-gson:2.4.0'
    api 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    api 'com.facebook.android:facebook-login:[4,5)'
    api 'com.google.android.gms:play-services-auth:15.0.1'

    implementation 'com.android.support:recyclerview-v7:27.1.1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
}

apply from: 'gradle/gradle-bintray-push.gradle'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
